---
page_title: "Sanity Terraform Provider"
description: |-
  A Terraform provider for Sanity that allows teams to configure and update CMS resources.
---

# Sanity Terraform Provider

The Sanity Terraform Provider allows you to configure and update CMS resources for [Sanity](https://www.sanity.io/).

## Setup

### Authentication

The provider requires an authentication token used to communicate with Sanity APIs. You can use a token created as part of a project, or you can use the auth token created when you sign in to the Sanity CLI. To get your auth token you can run `sanity debug --secrets` at a command prompt.

```terraform
provider "sanity" {
  token = var.sanity_auth_token
}
```

However, it is best to not store the token in plain text. Instead, you can set an environment variable `SANITY_TOKEN` with the auth token, and then you can omit the `token` from the provider block:

```terraform
provider "sanity" {}
```

## Example Usage

```terraform
# Configure the Sanity Provider
provider "sanity" {
  token = var.sanity_auth_token
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `token` (String, Sensitive) The auth token used to authenticate with Sanity. May be sourced from the `SANITY_TOKEN` environment variable instead of via this attribute.



## Typical Usage

Below is a full example showcasing typical usage:

```terraform
terraform {
  required_providers {
    sanity = {
      source = "tessellator/sanity"
    }
  }
  required_version = ">= 1.0"
}

provider "sanity" {
  token = "xxx"
}

resource "sanity_project" "my_blog" {
  name        = "My blog"
  color       = "#0000ff"
  studio_host = "my-cool-blog"

  lifecycle {
    prevent_destroy = true
  }
}

resource "sanity_cors_origin" "external" {
  project           = sanity_project.my_blog.id
  origin            = "https://example.com"
  allow_credentials = true
}

resource "sanity_dataset" "production" {
  project  = sanity_project.my_blog.id
  name     = "production"
  acl_mode = "public"

  lifecycle {
    prevent_destroy = true
  }
}

resource "sanity_project_token" "deployer" {
  project   = sanity_project.my_blog.id
  label     = "Deployer token"
  role_name = "deploy-studio"
}
```
